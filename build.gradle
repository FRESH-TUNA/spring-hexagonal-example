buildscript {
    ext {
        junitVersion = '5.3.2'
        assertjVersion = '3.11.1'
        vavrVersion = '0.9.2'
        lombokVersion = '1.18.4'
        mockitoVersion = '2.23.0'
        pitestJunit5PluginVersion = '0.8'
        pitestPluginVersion = '1.3.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("info.solidsoft.gradle.pitest:gradle-pitest-plugin:$pitestPluginVersion")
    }
}

allprojects {
    group 'com.rdelgatte.hexagonal'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaPlugin) {
        dependencies {
            annotationProcessor "org.projectlombok:lombok:$lombokVersion"
            compileOnly "org.projectlombok:lombok:$lombokVersion"
            testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
            testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
            implementation "io.vavr:vavr:$vavrVersion"
            testImplementation "org.assertj:assertj-core:$assertjVersion"
            testImplementation "org.mockito:mockito-core:$mockitoVersion"
            testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
            testImplementation "org.pitest:pitest-junit5-plugin:$pitestJunit5PluginVersion"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    // Configuring jacoco version to use with JDK 11
    jacoco {
        toolVersion = "0.8.2"
    }

    // Configuring test to use Junit5
    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}


